//! Graphics API wrapper for OpenGL.
pub const OpenGL = @This();

const std = @import("std");
const global = @import("../global.zig");
const Allocator = std.mem.Allocator;
const builtin = @import("builtin");
const gl = @import("opengl");
const shadertoy = @import("shadertoy.zig");
const apprt = @import("../apprt.zig");
const font = @import("../font/main.zig");
const configpkg = @import("../config.zig");
const perf = @import("../perf.zig");
const rendererpkg = @import("../renderer.zig");
const Renderer = rendererpkg.GenericRenderer(OpenGL);

pub const GraphicsAPI = OpenGL;
pub const Target = @import("opengl/Target.zig");
pub const Frame = @import("opengl/Frame.zig");
pub const RenderPass = @import("opengl/RenderPass.zig");
pub const Pipeline = @import("opengl/Pipeline.zig");
const bufferpkg = @import("opengl/buffer.zig");
pub const Buffer = bufferpkg.Buffer;
pub const Sampler = @import("opengl/Sampler.zig");
pub const Texture = @import("opengl/Texture.zig");
pub const shaders = @import("opengl/shaders.zig");

pub const custom_shader_target: shadertoy.Target = .glsl;
// The fragCoord for OpenGL shaders is +Y = up.
pub const custom_shader_y_is_down = false;

/// Because OpenGL's frame completion is always
/// sync, we have no need for multi-buffering.
pub const swap_chain_count = 1;

const log = std.log.scoped(.opengl);

/// We require at least OpenGL 4.3
pub const MIN_VERSION_MAJOR = 4;
pub const MIN_VERSION_MINOR = 3;

alloc: std.mem.Allocator,

/// Alpha blending mode
blending: configpkg.Config.AlphaBlending,

/// Whether presents are throttled to the display refresh
/// (window-vsync; used by the win32 flip-model path).
vsync: bool,

/// Whether the win32 flip-model presentation path is enabled
/// (windows-flip-model; the classic SwapBuffers path is the default
/// for its measured typing latency).
flip_model: bool,

/// The most recently presented target, in case we need to present it again.
last_target: ?Target = null,

/// NOTE: This is an error{}!OpenGL instead of just OpenGL for parity with
///       Metal, since it needs to be fallible so does this, even though it
///       can't actually fail.
pub fn init(alloc: Allocator, opts: rendererpkg.Options) error{}!OpenGL {
    return .{
        .alloc = alloc,
        .blending = opts.config.blending,
        .vsync = opts.config.vsync,
        .flip_model = opts.config.windows_flip_model,
    };
}

pub fn deinit(self: *OpenGL) void {
    self.* = undefined;
}

/// 32-bit windows cross-compilation breaks with `.c` for some reason, so...
const gl_debug_proc_callconv =
    @typeInfo(
        @typeInfo(
            @typeInfo(
                gl.c.GLDEBUGPROC,
            ).optional.child,
        ).pointer.child,
    ).@"fn".calling_convention;

fn glDebugMessageCallback(
    src: gl.c.GLenum,
    typ: gl.c.GLenum,
    id: gl.c.GLuint,
    severity: gl.c.GLenum,
    len: gl.c.GLsizei,
    msg: [*c]const gl.c.GLchar,
    user_param: ?*const anyopaque,
) callconv(gl_debug_proc_callconv) void {
    _ = user_param;

    const src_str: []const u8 = switch (src) {
        gl.c.GL_DEBUG_SOURCE_API => "OpenGL API",
        gl.c.GL_DEBUG_SOURCE_WINDOW_SYSTEM => "Window System",
        gl.c.GL_DEBUG_SOURCE_SHADER_COMPILER => "Shader Compiler",
        gl.c.GL_DEBUG_SOURCE_THIRD_PARTY => "Third Party",
        gl.c.GL_DEBUG_SOURCE_APPLICATION => "User",
        gl.c.GL_DEBUG_SOURCE_OTHER => "Other",
        else => "Unknown",
    };

    const typ_str: []const u8 = switch (typ) {
        gl.c.GL_DEBUG_TYPE_ERROR => "Error",
        gl.c.GL_DEBUG_TYPE_DEPRECATED_BEHAVIOR => "Deprecated Behavior",
        gl.c.GL_DEBUG_TYPE_UNDEFINED_BEHAVIOR => "Undefined Behavior",
        gl.c.GL_DEBUG_TYPE_PORTABILITY => "Portability Issue",
        gl.c.GL_DEBUG_TYPE_PERFORMANCE => "Performance Issue",
        gl.c.GL_DEBUG_TYPE_MARKER => "Marker",
        gl.c.GL_DEBUG_TYPE_PUSH_GROUP => "Group Push",
        gl.c.GL_DEBUG_TYPE_POP_GROUP => "Group Pop",
        gl.c.GL_DEBUG_TYPE_OTHER => "Other",
        else => "Unknown",
    };

    const msg_str = msg[0..@intCast(len)];

    (switch (severity) {
        gl.c.GL_DEBUG_SEVERITY_HIGH => log.err(
            "[{d}] ({s}: {s}) {s}",
            .{ id, src_str, typ_str, msg_str },
        ),
        gl.c.GL_DEBUG_SEVERITY_MEDIUM => log.warn(
            "[{d}] ({s}: {s}) {s}",
            .{ id, src_str, typ_str, msg_str },
        ),
        gl.c.GL_DEBUG_SEVERITY_LOW => log.info(
            "[{d}] ({s}: {s}) {s}",
            .{ id, src_str, typ_str, msg_str },
        ),
        gl.c.GL_DEBUG_SEVERITY_NOTIFICATION => log.debug(
            "[{d}] ({s}: {s}) {s}",
            .{ id, src_str, typ_str, msg_str },
        ),
        else => log.warn(
            "UNKNOWN SEVERITY [{d}] ({s}: {s}) {s}",
            .{ id, src_str, typ_str, msg_str },
        ),
    });
}

/// Prepares the provided GL context, loading it with glad.
fn prepareContext(getProcAddress: anytype) !void {
    const version = try gl.glad.load(getProcAddress);
    const major = gl.glad.versionMajor(@intCast(version));
    const minor = gl.glad.versionMinor(@intCast(version));
    errdefer gl.glad.unload();
    log.info("loaded OpenGL {}.{}", .{ major, minor });

    // Need to check version before trying to enable it
    if (major < MIN_VERSION_MAJOR or
        (major == MIN_VERSION_MAJOR and minor < MIN_VERSION_MINOR))
    {
        log.warn(
            "OpenGL version is too old. Ghostty requires OpenGL {d}.{d}",
            .{ MIN_VERSION_MAJOR, MIN_VERSION_MINOR },
        );
        return error.OpenGLOutdated;
    }

    // Enable debug output for the context.
    try gl.enable(gl.c.GL_DEBUG_OUTPUT);

    // Register our debug message callback with the OpenGL context.
    gl.glad.context.DebugMessageCallback.?(glDebugMessageCallback, null);

    // Enable SRGB framebuffer for linear blending support.
    try gl.enable(gl.c.GL_FRAMEBUFFER_SRGB);
}

/// This is called early right after surface creation.
pub fn surfaceInit(surface: *apprt.Surface) !void {
    switch (apprt.runtime) {
        else => @compileError("unsupported app runtime for OpenGL"),

        // GTK uses global OpenGL context so we load from null.
        apprt.gtk,
        => try prepareContext(null),

        apprt.embedded => {
            // TODO(mitchellh): this does nothing today to allow libghostty
            // to compile for OpenGL targets but libghostty is strictly
            // broken for rendering on this platforms.
        },

        // The WGL context was created with the window; make it current
        // on this (main) thread for any GL the core does during surface
        // init. finalizeSurfaceInit releases it for the renderer thread.
        apprt.win32 => {
            try surface.glMakeCurrent();
            try prepareContext(&apprt.win32.winapi.glGetProcAddress);
        },
    }

    // These are very noisy so this is commented, but easy to uncomment
    // whenever we need to check the OpenGL extension list
    // if (builtin.mode == .Debug) {
    //     var ext_iter = try gl.ext.iterator();
    //     while (try ext_iter.next()) |ext| {
    //         log.debug("OpenGL extension available name={s}", .{ext});
    //     }
    // }
}

/// This is called just prior to spinning up the renderer
/// thread for final main thread setup requirements.
pub fn finalizeSurfaceInit(self: *const OpenGL, surface: *apprt.Surface) !void {
    _ = self;
    _ = surface;

    // Release the WGL context from the main thread so the renderer
    // thread can make it current in threadEnter.
    if (comptime apprt.runtime == apprt.win32) {
        apprt.win32.Surface.glReleaseCurrent();
    }
}

/// Callback called by renderer.Thread when it begins.
pub fn threadEnter(self: *const OpenGL, surface: *apprt.Surface) !void {
    switch (apprt.runtime) {
        else => @compileError("unsupported app runtime for OpenGL"),

        apprt.gtk => {
            // GTK doesn't support threaded OpenGL operations as far as I can
            // tell, so we use the renderer thread to setup all the state
            // but then do the actual draws and texture syncs and all that
            // on the main thread. As such, we don't do anything here.
        },

        apprt.embedded => {
            // TODO(mitchellh): this does nothing today to allow libghostty
            // to compile for OpenGL targets but libghostty is strictly
            // broken for rendering on this platforms.
        },

        // Take the WGL context on the renderer thread; all drawing
        // happens here (no must_draw_from_app_thread). The glad
        // bindings are threadlocal so they must be reloaded.
        apprt.win32 => {
            try surface.glMakeCurrent();
            try prepareContext(&apprt.win32.winapi.glGetProcAddress);

            // Two presentation paths, selected by windows-flip-model:
            //
            // Classic (default): SwapBuffers into the window's
            // redirected surface. Camera-measured typing latency
            // favors this class of presentation (GDI conhost is the
            // fastest-measured Windows terminal; all flip/GPU
            // terminals measured ~2x slower) and our own PresentMon
            // numbers agree. Always vsync-throttled: sporadic typing
            // presents never block on the interval anyway, sustained
            // bursts throttle at the driver, and unthrottled
            // SwapBuffers once ran the GPU hot enough to trigger
            // driver timeouts (LiveKernelEvent 141).
            //
            // Flip-model (opt-in): DXGI swapchain on a DComp visual
            // (the Windows Terminal architecture), eligible for
            // hardware-overlay promotion, and the foundation for
            // future per-pixel transparency.
            if (self.flip_model and initPresenter(surface)) {
                log.info("flip-model presentation active", .{});
            } else {
                if (!apprt.win32.winapi.setSwapInterval(1)) {
                    log.warn("wglSwapIntervalEXT unavailable; presentation unthrottled", .{});
                }
            }
        },
    }
}

/// Callback called by renderer.Thread when it exits.
pub fn threadExit(self: *const OpenGL) void {
    _ = self;

    switch (apprt.runtime) {
        else => @compileError("unsupported app runtime for OpenGL"),

        apprt.gtk => {
            // We don't need to do any unloading for GTK because we may
            // be sharing the global bindings with other windows.
        },

        apprt.embedded => {
            // TODO: see threadEnter
        },

        apprt.win32 => {
            // Tear down flip-model state while the context is still
            // current (the interop handles are bound to it).
            if (currentWin32Surface()) |surface| deinitPresenter(surface);
            apprt.win32.Surface.glReleaseCurrent();
        },
    }
}

/// The apprt surface owning the current WGL context, recovered from
/// the host window (the renderer thread's only route back).
fn currentWin32Surface() ?*apprt.win32.Surface {
    const winapi = apprt.win32.winapi;
    const hdc = winapi.wglGetCurrentDC() orelse return null;
    const hwnd = winapi.WindowFromDC(hdc) orelse return null;
    const ptr = winapi.GetWindowLongPtrW(hwnd, winapi.GWLP_USERDATA);
    if (ptr == 0) return null;
    return @ptrFromInt(@as(usize, @bitCast(ptr)));
}

/// Set up flip-model presentation for a surface: swapchain, interop,
/// and the GL renderbuffer+FBO pair aliasing the backbuffer. Returns
/// false (leaving the surface on the SwapBuffers path) on any failure.
fn initPresenter(surface: *apprt.Surface) bool {
    if (comptime apprt.runtime != apprt.win32) return false;
    const winapi = apprt.win32.winapi;

    // Escape hatch for benchmarking and driver-issue workarounds.
    if (global.environ().getWindows(std.unicode.utf8ToUtf16LeStringLiteral("GHOSTTY_NO_FLIP")) != null) return false;

    var client: winapi.RECT = undefined;
    if (winapi.GetClientRect(surface.host, &client) == 0) return false;

    var presenter = apprt.win32.Surface.dxgi.Presenter.init(
        surface.host,
        @intCast(@max(1, client.right - client.left)),
        @intCast(@max(1, client.bottom - client.top)),
    ) catch |err| {
        // With windows-flip-model on, the host window was created with
        // WS_EX_NOREDIRECTIONBITMAP (creation-only): SwapBuffers has no
        // redirection surface to present into, so this fallback shows a
        // blank window. The startup probe exercises the full presenter
        // pipeline to make this unreachable in practice; if it fires
        // anyway, say so loudly rather than failing silently.
        log.err(
            "flip-model presenter failed at runtime err={}; the SwapBuffers " ++
                "fallback cannot display into a WS_EX_NOREDIRECTIONBITMAP host — " ++
                "if this window is blank, unset windows-flip-model",
            .{err},
        );
        return false;
    };

    if (!attachBackbuffer(&presenter)) {
        presenter.deinit();
        return false;
    }

    surface.presenter = presenter;
    return true;
}

fn deinitPresenter(surface: *apprt.win32.Surface) void {
    if (surface.presenter) |*p| {
        const ctx = gl.glad.context;
        if (p.fbo != 0) ctx.DeleteFramebuffers.?(1, &p.fbo);
        if (p.renderbuffer != 0) ctx.DeleteRenderbuffers.?(1, &p.renderbuffer);
        p.deinit();
        surface.presenter = null;
    }
}

/// (Re)create the GL renderbuffer aliasing the swapchain backbuffer
/// and the FBO wrapping it.
fn attachBackbuffer(p: *apprt.win32.Surface.dxgi.Presenter) bool {
    const ctx = gl.glad.context;

    if (p.renderbuffer == 0) ctx.GenRenderbuffers.?(1, &p.renderbuffer);
    if (p.fbo == 0) ctx.GenFramebuffers.?(1, &p.fbo);

    p.acquireBackbuffer(p.renderbuffer) catch |err| {
        log.warn("backbuffer interop failed err={}", .{err});
        return false;
    };

    ctx.BindFramebuffer.?(gl.c.GL_FRAMEBUFFER, p.fbo);
    ctx.FramebufferRenderbuffer.?(
        gl.c.GL_FRAMEBUFFER,
        gl.c.GL_COLOR_ATTACHMENT0,
        gl.c.GL_RENDERBUFFER,
        p.renderbuffer,
    );
    ctx.BindFramebuffer.?(gl.c.GL_FRAMEBUFFER, 0);
    return true;
}

pub fn displayRealized(self: *const OpenGL) void {
    _ = self;

    switch (apprt.runtime) {
        apprt.gtk => prepareContext(null) catch |err| {
            log.warn(
                "Error preparing GL context in displayRealized, err={}",
                .{err},
            );
        },

        else => @compileError("only GTK should be calling displayRealized"),
    }
}

/// Actions taken before doing anything in `drawFrame`.
pub fn drawFrameStart(self: *OpenGL) void {
    _ = self;

    // On win32 we own the GL surface, so we are responsible for keeping
    // the viewport in sync with the window's client area (GTK's GLArea
    // does this implicitly). surfaceSize() reads the viewport back, so
    // this is also how drawFrame learns about resizes. The window is
    // recovered from the current DC to avoid plumbing a surface pointer
    // through the renderer.
    if (comptime apprt.runtime == apprt.win32) {
        const winapi = apprt.win32.winapi;
        const hdc = winapi.wglGetCurrentDC() orelse return;
        const hwnd = winapi.WindowFromDC(hdc) orelse return;
        var rect: winapi.RECT = undefined;
        if (winapi.GetClientRect(hwnd, &rect) == 0) return;
        gl.glad.context.Viewport.?(
            0,
            0,
            rect.right - rect.left,
            rect.bottom - rect.top,
        );

        // Flip-model: track window resizes with the swapchain.
        if (currentWin32Surface()) |surface| {
            if (surface.presenter) |*p| {
                const w: u32 = @intCast(@max(1, rect.right - rect.left));
                const h: u32 = @intCast(@max(1, rect.bottom - rect.top));
                if (w != p.width or h != p.height) resize: {
                    p.resize(w, h) catch |err| {
                        log.warn("swapchain resize failed err={}", .{err});
                        deinitPresenter(surface);
                        break :resize;
                    };
                    if (!attachBackbuffer(p)) {
                        deinitPresenter(surface);
                        break :resize;
                    }
                    // The frame being drawn may have been sampled at
                    // the old size (the resize message races this
                    // callback) and the resized GL framebuffer's
                    // content is undefined; queue a full follow-up
                    // render so a stale/blank frame can't be the last
                    // thing presented.
                    surface.core_surface.refreshCallback() catch {};
                }
            }
        }
    }
}

/// Actions taken after `drawFrame` is done.
pub fn drawFrameEnd(self: *OpenGL) void {
    // We own the swap chain on win32: present the default framebuffer.
    // Hidden windows (background tabs) skip presentation entirely; with
    // vsync on, presenting an invisible window would also block this
    // renderer thread on the compositor for no benefit.
    if (comptime apprt.runtime == apprt.win32) {
        const winapi = apprt.win32.winapi;
        if (winapi.wglGetCurrentDC()) |hdc| {
            const hwnd = winapi.WindowFromDC(hdc);
            if (hwnd != null and winapi.IsWindowVisible(hwnd.?) == 0) return;

            present: {
                // Flip-model: copy the rendered frame (GL default
                // framebuffer, bottom-left origin) into the swapchain
                // backbuffer (top-left origin: flipped blit) and
                // present without queueing.
                if (currentWin32Surface()) |surface| {
                    if (surface.presenter) |*p| {
                        if (!p.lock()) {
                            log.warn("interop lock failed; dropping flip-model", .{});
                            deinitPresenter(surface);
                            break :present;
                        }

                        const ctx = gl.glad.context;
                        ctx.BindFramebuffer.?(gl.c.GL_READ_FRAMEBUFFER, 0);
                        ctx.BindFramebuffer.?(gl.c.GL_DRAW_FRAMEBUFFER, p.fbo);
                        const w: i32 = @intCast(p.width);
                        const h: i32 = @intCast(p.height);
                        ctx.BlitFramebuffer.?(
                            0,
                            0,
                            w,
                            h,
                            0,
                            h,
                            w,
                            0,
                            gl.c.GL_COLOR_BUFFER_BIT,
                            gl.c.GL_NEAREST,
                        );
                        ctx.BindFramebuffer.?(gl.c.GL_FRAMEBUFFER, 0);

                        p.unlock();
                        if (!p.present(self.vsync)) {
                            // Device removed/reset (GPU TDR, driver
                            // update): the swapchain is dead. Rebuild
                            // the presenter on a fresh device; if that
                            // fails, fall back to SwapBuffers rather
                            // than freezing on a dead swapchain.
                            log.warn("device lost; rebuilding presenter", .{});
                            deinitPresenter(surface);
                            if (!initPresenter(surface)) {
                                log.warn(
                                    "presenter rebuild failed; using SwapBuffers",
                                    .{},
                                );
                            }
                            // The new backbuffer holds no frame yet.
                            surface.core_surface.refreshCallback() catch {};
                            break :present;
                        }

                        if (perf.keyToPresent()) |ns| {
                            log.info("perf: key-to-present {d} us", .{ns / 1000});
                        }
                        return;
                    }
                }
            }

            // Legacy path.
            if (winapi.SwapBuffers(hdc) == 0) {
                log.warn("SwapBuffers failed", .{});
            }

            // Key-to-present latency tracing (GHOSTTY_PERF_TRACE).
            // This is the full echo path: key encode, pty write,
            // shell echo, ConPTY, parse, damage, render, present.
            if (perf.keyToPresent()) |ns| {
                log.info("perf: key-to-present {d} us", .{ns / 1000});
            }
            if (perf.sinceKeyMs()) |ms| {
                log.info("perf: present key+{d}ms", .{ms});
            }
        }
    }
}

pub fn initShaders(
    self: *const OpenGL,
    alloc: Allocator,
    custom_shaders: []const [:0]const u8,
) !shaders.Shaders {
    _ = alloc;
    return try shaders.Shaders.init(
        self.alloc,
        custom_shaders,
    );
}

/// Get the current size of the runtime surface.
pub fn surfaceSize(self: *const OpenGL) !struct { width: u32, height: u32 } {
    _ = self;
    var viewport: [4]gl.c.GLint = undefined;
    gl.glad.context.GetIntegerv.?(gl.c.GL_VIEWPORT, &viewport);
    return .{
        .width = @intCast(viewport[2]),
        .height = @intCast(viewport[3]),
    };
}

/// Initialize a new render target which can be presented by this API.
pub fn initTarget(self: *const OpenGL, width: usize, height: usize) !Target {
    return Target.init(.{
        .internal_format = if (self.blending.isLinear()) .srgba else .rgba,
        .width = width,
        .height = height,
    });
}

/// Present the provided target.
pub fn present(self: *OpenGL, target: Target) !void {
    // In order to present a target we blit it to the default framebuffer.

    // We disable GL_FRAMEBUFFER_SRGB while doing this blit, otherwise the
    // values may be linearized as they're copied, but even though the draw
    // framebuffer has a linear internal format, the values in it should be
    // sRGB, not linear!
    try gl.disable(gl.c.GL_FRAMEBUFFER_SRGB);
    defer gl.enable(gl.c.GL_FRAMEBUFFER_SRGB) catch |err| {
        log.err("Error re-enabling GL_FRAMEBUFFER_SRGB, err={}", .{err});
    };

    // Bind the target for reading.
    const fbobind = try target.framebuffer.bind(.read);
    defer fbobind.unbind();

    // Blit
    gl.glad.context.BlitFramebuffer.?(
        0,
        0,
        @intCast(target.width),
        @intCast(target.height),
        0,
        0,
        @intCast(target.width),
        @intCast(target.height),
        gl.c.GL_COLOR_BUFFER_BIT,
        gl.c.GL_NEAREST,
    );

    // Keep track of this target in case we need to repeat it.
    self.last_target = target;
}

/// Present the last presented target again.
pub fn presentLastTarget(self: *OpenGL) !void {
    if (self.last_target) |target| try self.present(target);
}

/// Returns the options to use when constructing buffers.
pub inline fn bufferOptions(self: OpenGL) bufferpkg.Options {
    _ = self;
    return .{
        .target = .array,
        .usage = .dynamic_draw,
    };
}

pub const instanceBufferOptions = bufferOptions;
pub const uniformBufferOptions = bufferOptions;
pub const fgBufferOptions = bufferOptions;
pub const bgBufferOptions = bufferOptions;
pub const imageBufferOptions = bufferOptions;
pub const bgImageBufferOptions = bufferOptions;

/// Returns the options to use when constructing textures.
pub inline fn textureOptions(self: OpenGL) Texture.Options {
    _ = self;
    return .{
        .format = .rgba,
        .internal_format = .srgba,
        .target = .@"2D",
        .min_filter = .linear,
        .mag_filter = .linear,
        .wrap_s = .clamp_to_edge,
        .wrap_t = .clamp_to_edge,
    };
}

/// Returns the options to use when constructing samplers.
pub inline fn samplerOptions(self: OpenGL) Sampler.Options {
    _ = self;
    return .{
        .min_filter = .linear,
        .mag_filter = .linear,
        .wrap_s = .clamp_to_edge,
        .wrap_t = .clamp_to_edge,
    };
}

/// Pixel format for image texture options.
pub const ImageTextureFormat = enum {
    /// 1 byte per pixel grayscale.
    gray,
    /// 4 bytes per pixel RGBA.
    rgba,
    /// 4 bytes per pixel BGRA.
    bgra,

    fn toPixelFormat(self: ImageTextureFormat) gl.Texture.Format {
        return switch (self) {
            .gray => .red,
            .rgba => .rgba,
            .bgra => .bgra,
        };
    }
};

/// Returns the options to use when constructing textures for images.
pub inline fn imageTextureOptions(
    self: OpenGL,
    format: ImageTextureFormat,
    srgb: bool,
) Texture.Options {
    _ = self;
    return .{
        .format = format.toPixelFormat(),
        .internal_format = if (srgb) .srgba else .rgba,
        .target = .@"2D",
        // TODO: Generate mipmaps for image textures and use
        //       linear_mipmap_linear filtering so that they
        //       look good even when scaled way down.
        .min_filter = .linear,
        .mag_filter = .linear,
        // TODO: Separate out background image options, use
        //       repeating coordinate modes so we don't have
        //       to do the modulus in the shader.
        .wrap_s = .clamp_to_edge,
        .wrap_t = .clamp_to_edge,
    };
}

/// Initializes a Texture suitable for the provided font atlas.
pub fn initAtlasTexture(
    self: *const OpenGL,
    atlas: *const font.Atlas,
) Texture.Error!Texture {
    _ = self;
    const format: gl.Texture.Format, const internal_format: gl.Texture.InternalFormat =
        switch (atlas.format) {
            .grayscale => .{ .red, .red },
            .bgra => .{ .bgra, .srgba },
            else => @panic("unsupported atlas format for OpenGL texture"),
        };

    return try Texture.init(
        .{
            .format = format,
            .internal_format = internal_format,
            .target = .Rectangle,
            .min_filter = .nearest,
            .mag_filter = .nearest,
            .wrap_s = .clamp_to_edge,
            .wrap_t = .clamp_to_edge,
        },
        atlas.size,
        atlas.size,
        null,
    );
}

/// Begin a frame.
pub inline fn beginFrame(
    self: *const OpenGL,
    /// Once the frame has been completed, the `frameCompleted` method
    /// on the renderer is called with the health status of the frame.
    renderer: *Renderer,
    /// The target is presented via the provided renderer's API when completed.
    target: *Target,
) !Frame {
    _ = self;
    return try Frame.begin(.{}, renderer, target);
}
